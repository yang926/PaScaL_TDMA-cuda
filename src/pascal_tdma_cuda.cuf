!======================================================================================================================
!> @file        pascal_tdma.f90
!> @brief       PaScaL_TDMA - Parallel and Scalable Library for TriDiagonal Matrix Algorithm
!> @details     PaScal_TDMA provides an efficient and scalable computational procedure 
!>              to solve many tridiagonal systems in multi-dimensional partial differential equations. 
!>              The modified Thomas algorithm proposed by Laszlo et al.(2016) and the newly designed communication 
!>              scheme have been used to reduce the communication overhead in solving many tridiagonal systems.
!>              This library is for both single and many tridiagonal systems of equations. 
!>              The main algorithm for a tridiagonal matrix consists of the following five steps: 
!>
!>              (1) Transform the partitioned submatrices in the tridiagonal systems into modified submatrices:
!>                  Each computing core transforms the partitioned submatrices in the tridiagonal systems 
!>                  of equations into modified forms by applying the modified Thomas algorithm.
!>              (2) Construct reduced tridiagonal systems from the modified submatrices:
!>                  The reduced tridiagonal systems are constructed by collecting the first and last rows 
!>                  of the modified submatrices from each core using MPI_Ialltoallw.
!>              (3) Solve the reduced tridiagonal systems:
!>                  The reduced tridiagonal systems constructed in Step 2 are solved by applying the Thomas algorithm.
!>              (4) Distribute the solutions of the reduced tridiagonal systems:
!>                  The solutions of the reduced tridiagonal systems in Step 3 are distributed to each core 
!>                  using MPI_Ialltoallw. This communication is an exact inverse of the communication in Step 2.
!>              (5) Update the other unknowns in the modified tridiagonal systems:
!>                  The remaining unknowns in the modified submatrices in Step 1 are solved in each computing core 
!>                  using the solutions obtained in Step 3 and Step 4.
!>
!>              Step 1 and Step 5 are similar to the method proposed by Laszlo et al.(2016)
!>              which uses parallel cyclic reduction (PCR) algorithm to build and solve the reduced tridiagonal systems.
!>              Instead of using the PCR, we develop an all-to-all communication scheme using the MPI_Ialltoall
!>              function after the modified Thomas algorithm is executed. The number of coefficients for
!>              the reduced tridiagonal systems are greatly reduced, so we can avoid the communication 
!>              bandwidth problem, which is a main bottleneck for all-to-all communications.
!>              Our algorithm is also distinguished from the work of Mattor et al. (1995) which
!>              assembles the undetermined coefficients of the temporary solutions in a single processor 
!>              using MPI_Gather, where load imbalances are serious.
!> 
!> @author      
!>              - Kiha Kim (k-kiha@yonsei.ac.kr), Department of Computational Science & Engineering, Yonsei University
!>              - Ji-Hoon Kang (jhkang@kisti.re.kr), Korea Institute of Science and Technology Information
!>              - Jung-Il Choi (jic@yonsei.ac.kr), Department of Computational Science & Engineering, Yonsei University
!>
!> @date        June 2019
!> @version     1.0
!> @par         Copyright
!>              Copyright (c) 2019 Kiha Kim and Jung-Il choi, Yonsei University and 
!>              Ji-Hoon Kang, Korea Institute of Science and Technology Information, All rights reserved.
!> @par         License     
!>              This project is released under the terms of the MIT License (see LICENSE )
!======================================================================================================================

!>
!> @brief       Module for PaScaL-TDMA library.
!> @details     It contains plans for tridiagonal systems of equations and subroutines for solving them 
!>              using the defined plans. The operation of the library includes the following three phases:
!>              (1) Create a data structure called a plan, which has the information for communication and reduced systems.
!>              (2) Solve the tridiagonal systems of equations executing from Step 1 to Step 5
!>              (3) Destroy the created plan
!>
module PaScaL_TDMA_cuda

    use mpi
    use omp_lib
    use cudafor

    implicit none

    !> @brief   Execution plan for many tridiagonal systems of equations.
    !> @details It uses MPI_Ialltoallw function to distribute the modified tridiagonal systems to MPI processes
    !>          and build the reduced tridiagonal systems of equations. Derived datatypes are defined and used 
    !>          to eliminate the cost of data packing and unpacking.
    type, public :: ptdma_plan_many_cuda

        integer :: ptdma_world      !< Single dimensional subcommunicator to assemble data for the reduced TDMA
        integer :: ptdma_nprocs     !< Size of ptdma_world
        integer :: n_sys_rt         !< Number of tridiagonal systems that need to be solved in each process after transpose
        integer :: n_row_rt         !< Number of rows of a reduced tridiagonal systems after transpose

        !> @{ Send buffer related variables for MPI_Ialltoallw
        integer, allocatable, dimension(:) :: ddtype_FS, count_send, displ_send
        !> @}

        !> @{ Recv. buffer related variables MPI_Ialltoallw 
        integer, allocatable, dimension(:) :: ddtype_BS, count_recv, displ_recv
        !> @}

        !> @{ Coefficient arrays after reduction, a: lower, b: diagonal, c: upper, d: rhs.
        !>    The orginal dimension (m:n) is reduced to (m:2)
        double precision, allocatable, dimension(:,:,:) :: A_rd, B_rd, C_rd, D_rd     
        !> @}

        !> @{ Coefficient arrays after transpose of reduced systems, a: lower, b: diagonal, c: upper, d: rhs
        !>    The reduced dimension (m:2) changes to (m/np: 2*np) after transpose.
        double precision, allocatable, dimension(:,:,:) :: A_rt, B_rt, C_rt, D_rt 
        !> @}

        double precision, allocatable, device, dimension(:,:,:) :: a_rd_d, b_rd_d, c_rd_d, d_rd_d
        double precision, allocatable, device, dimension(:,:,:) :: a_rt_d, b_rt_d, c_rt_d, d_rt_d, e_rt_d
    
    end type ptdma_plan_many_cuda

    integer, parameter, private :: thread_in_x = 32, thread_in_y = 16
    integer, private :: block_in_x, block_rt_in_x, block_in_y

    public  :: PaScaL_TDMA_plan_many_create_cuda
    public  :: PaScaL_TDMA_plan_many_destroy_cuda
    public  :: PaScaL_TDMA_many_solve_cuda
    public  :: PaScaL_TDMA_many_solve_cycle_cuda 

    contains

    !>
    !> @brief   Create a plan for many tridiagonal systems of equations.
    !> @param   plan        Plan for a single tridiagonal system of equations
    !> @param   n_sys       Number of tridiagonal systems of equations for process
    !> @param   myrank      Rank ID in mpi_world
    !> @param   nprocs      Number of MPI process in mpi_world
    !> @param   mpi_world   Communicator for MPI_Gather and MPI_Scatter of reduced equations
    !>
    subroutine PaScaL_TDMA_plan_many_create_cuda(plan, n_sys, n_row, n_blk, myrank, nprocs, mpi_world)

        implicit none

        type(ptdma_plan_many_cuda), intent(inout)  :: plan
        integer, intent(in)     :: n_row, n_sys
        integer, intent(in)     :: n_blk
        integer, intent(in)     :: myrank, nprocs, mpi_world

        integer :: i, ierr
        integer :: ista, iend                               ! First and last indices of assigned range in many tridiagonal systems of equations 
        integer :: bigsize(3), subsize(3), start(3)         ! Temporary variables of derived data type (DDT)
        integer :: ns_rd, nr_rd                             ! Dimensions of many reduced tridiagonal systems
        integer :: ns_rt, nr_rt                             ! Dimensions of many reduced tridiagonal systems after transpose
        integer, allocatable, dimension(:):: ns_rt_array    ! Array specifying the number of tridiagonal systems for each process after transpose

        ! Specify dimensions for reduced systems.
        ns_rd = n_sys
        nr_rd = 2

        ! Specify dimensions for reduced systems after transpose.
        ! ns_rt         : divide the number of tridiagonal systems of equations per each process  
        ! ns_rt_array   : save the ns_rt in ns_rt_array for defining the DDT
        ! nr_rt         : dimensions of the reduced tridiagonal systems in the solving direction, nr_rd*nprocs
        call para_range(1, ns_rd, nprocs, myrank, ista, iend)
        ns_rt = iend - ista + 1
        allocate(ns_rt_array(0:nprocs-1))
        call MPI_Allgather(ns_rt, 1, mpi_integer, ns_rt_array,1, mpi_integer, mpi_world, ierr)
        nr_rt = nr_rd*nprocs

        ! Assign plan variables and allocate coefficient arrays.
        plan%n_sys_rt = ns_rt
        plan%n_row_rt = nr_rt
        plan%ptdma_world = mpi_world

        allocate( plan%A_rd(ns_rd, n_blk, nr_rd) )
        allocate( plan%B_rd(ns_rd, n_blk, nr_rd) )
        allocate( plan%C_rd(ns_rd, n_blk, nr_rd) )
        allocate( plan%D_rd(ns_rd, n_blk, nr_rd) )
        allocate( plan%A_rt(ns_rt, n_blk, nr_rt) )
        allocate( plan%B_rt(ns_rt, n_blk, nr_rt) )
        allocate( plan%C_rt(ns_rt, n_blk, nr_rt) )
        allocate( plan%D_rt(ns_rt, n_blk, nr_rt) )

        allocate( plan%a_rd_d(ns_rd, n_blk, nr_rd) )
        allocate( plan%b_rd_d(ns_rd, n_blk, nr_rd) )
        allocate( plan%c_rd_d(ns_rd, n_blk, nr_rd) )
        allocate( plan%d_rd_d(ns_rd, n_blk, nr_rd) )
        allocate( plan%a_rt_d(ns_rt, n_blk, nr_rt) )
        allocate( plan%b_rt_d(ns_rt, n_blk, nr_rt) )
        allocate( plan%c_rt_d(ns_rt, n_blk, nr_rt) )
        allocate( plan%d_rt_d(ns_rt, n_blk, nr_rt) )
        allocate( plan%e_rt_d(ns_rt, n_blk, nr_rt) )


        ! Building the DDTs.
        allocate(plan%ddtype_Fs(0:nprocs-1), plan%ddtype_Bs(0:nprocs-1))

        do i=0,nprocs-1
            ! DDT for sending coefficients of the reduced tridiagonal systems using MPI_Ialltoallw communication.
            bigsize(1)=ns_rd
            bigsize(2)=n_blk
            bigsize(3)=nr_rd
            subsize(1)=ns_rt_array(i)
            subsize(2)=n_blk
            subsize(3)=nr_rd
            start(1)=sum(ns_rt_array(0:i)) - ns_rt_array(i)
            start(2)=0
            start(3)=0
            call MPI_Type_create_subarray(  3, bigsize, subsize, start,                     &
                                            MPI_ORDER_FORTRAN, MPI_DOUBLE_PRECISION,        &
                                            plan%ddtype_Fs(i), ierr )
            call MPI_Type_commit(plan%ddtype_Fs(i), ierr)
            ! DDT for receiving coefficients for the transposed systems of reduction using MPI_Ialltoallw communication.
            bigsize(1)=ns_rt
            bigsize(2)=n_blk
            bigsize(3)=nr_rt
            subsize(1)=ns_rt
            subsize(2)=n_blk
            subsize(3)=nr_rd
            start(1)=0
            start(2)=0
            start(3)=nr_rd*i
            call MPI_Type_create_subarray(  3, bigsize, subsize, start,                     &
                                            MPI_ORDER_FORTRAN, MPI_DOUBLE_PRECISION,        &
                                            plan%ddtype_Bs(i), ierr )
            call MPI_Type_commit(plan%ddtype_Bs(i), ierr)
        enddo

        ! Buffer counts and displacements for MPI_Ialltoallw.
        ! All buffer counts are 1 and displacements are 0 due to the defined DDT.
        allocate(plan%count_send(0:nprocs-1), plan%displ_send(0:nprocs-1))
        allocate(plan%count_recv(0:nprocs-1), plan%displ_recv(0:nprocs-1))
        plan%count_send=1; plan%displ_send=0
        plan%count_recv=1; plan%displ_recv=0
        plan%ptdma_nprocs = nprocs

        ! Deallocate local array.
        if(allocated(ns_rt_array)) deallocate(ns_rt_array)

    end subroutine PaScaL_TDMA_plan_many_create_cuda

    !>
    !> @brief   Destroy the allocated arrays in the defined plan_many.
    !> @param   plan        Plan for many tridiagonal systems of equations
    !>
    subroutine PaScaL_TDMA_plan_many_destroy_cuda(plan,nprocs)
        implicit none

        type(ptdma_plan_many_cuda), intent(inout)  :: plan
        integer :: i,nprocs,ierr

        do i=0,nprocs-1
            call MPI_TYPE_FREE(plan%ddtype_Fs(i), ierr)
            call MPI_TYPE_FREE(plan%ddtype_Bs(i), ierr)
        enddo

        deallocate(plan%ddtype_Fs,  plan%ddtype_Bs)
        deallocate(plan%count_send, plan%displ_send)
        deallocate(plan%count_recv, plan%displ_recv)
        deallocate(plan%A_rd, plan%B_rd, plan%C_rd, plan%D_rd)
        deallocate(plan%A_rt, plan%B_rt, plan%C_rt, plan%D_rt)

        deallocate(plan%a_rd_d, plan%b_rd_d, plan%c_rd_d, plan%d_rd_d)
        deallocate(plan%a_rt_d, plan%b_rt_d, plan%c_rt_d, plan%d_rt_d, plan%e_rt_d)

    end subroutine PaScaL_TDMA_plan_many_destroy_cuda



    !>
    !> @brief   Solve many tridiagonal systems of equations.
    !> @param   plan        Plan for many tridiagonal systems of equations
    !> @param   A           Coefficients in lower diagonal elements
    !> @param   B           Coefficients in diagonal elements
    !> @param   C           Coefficients in upper diagonal elements
    !> @param   D           Coefficients in right-hand side terms
    !> @param   n_sys       Number of tridiagonal systems per process
    !> @param   n_row       Number of rows in each process, size of a tridiagonal matrix N divided by nprocs
    !>
    subroutine PaScaL_TDMA_many_solve_cuda(plan, a_d, b_d, c_d, d_d, n_sys, n_row, n_blk)

        implicit none

        type(ptdma_plan_many_cuda), intent(inout)   :: plan
        integer, value, intent(in)                  :: n_sys, n_row, n_blk
        double precision, device, intent(inout)     :: a_d(n_sys,n_blk,n_row), b_d(n_sys,n_blk,n_row)
        double precision, device, intent(inout)     :: c_d(n_sys,n_blk,n_row), d_d(n_sys,n_blk,n_row)

        ! Temporary variables for computation and parameters for MPI functions.
        integer :: request(4),ierr
        integer :: n_row_rt, n_sys_rt
        type(dim3) :: blocks, threads

        n_row_rt = plan%n_row_rt
        n_sys_rt = plan%n_sys_rt

        ! Setting the thread and block
        block_in_x = n_sys/thread_in_x
        if(block_in_x.eq.0 .or. mod(n_sys, thread_in_x)) then
            print '(a,i5,a,i5)', '[Error] n_sys should be a multiple of thread_in_x. thread_in_x = ',thread_in_x,', n_sys = ',n_sys
            call MPI_Finalize(ierr)
            stop
        endif

        block_rt_in_x = n_sys_rt/thread_in_x
        if(block_rt_in_x.eq.0 .or. mod(n_sys_rt, thread_in_x)) then
            print '(a,i5,a,i5)', '[Error] n_sys_rt should be a multiple of thread_in_x. thread_in_x = ',thread_in_x,', n_sys_rt = ',n_sys_rt
            call MPI_Finalize(ierr)
            stop
        endif

        block_in_y = n_blk/thread_in_y
        if(block_in_y.eq.0 .or. mod(n_blk, thread_in_y)) then
            print '(a,i5,a,i5)', '[Error] n_blk should be a multiple of thread_in_y. thread_in_y = ',thread_in_y,', n_blk = ',n_blk
            call MPI_Finalize(ierr)
            stop
        endif

        threads = dim3(thread_in_x, thread_in_y, 1)
        blocks  = dim3(block_in_x, block_in_y, 1)

        ! The modified Thomas algorithm
        call PaScaL_TDMA_many_modified_Thomas_cuda<<<blocks, threads>>>(a_d, b_d, c_d, d_d, plan%a_rd_d, plan%b_rd_d, plan%c_rd_d, plan%d_rd_d, n_row)

        ! Transpose the reduced systems of equations for TDMA using MPI_Ialltoallw and DDTs.
        if(plan%ptdma_nprocs.eq.1) then
            plan%a_rt_d = plan%a_rd_d
            plan%b_rt_d = plan%b_rd_d
            plan%c_rt_d = plan%c_rd_d
            plan%d_rt_d = plan%d_rd_d
        else
            plan%A_rd = plan%a_rd_d
            plan%B_rd = plan%b_rd_d
            plan%C_rd = plan%c_rd_d
            plan%D_rd = plan%d_rd_d

            call MPI_Ialltoallw(plan%A_rd, plan%count_send, plan%displ_send, plan%ddtype_Fs, &
                                plan%A_rt, plan%count_recv, plan%displ_recv, plan%ddtype_Bs, &
                                plan%ptdma_world, request(1), ierr)
            call MPI_Ialltoallw(plan%B_rd, plan%count_send, plan%displ_send, plan%ddtype_Fs, &
                                plan%B_rt, plan%count_recv, plan%displ_recv, plan%ddtype_Bs, &
                                plan%ptdma_world, request(2), ierr)
            call MPI_Ialltoallw(plan%C_rd, plan%count_send, plan%displ_send, plan%ddtype_Fs, &
                                plan%C_rt, plan%count_recv, plan%displ_recv, plan%ddtype_Bs, &
                                plan%ptdma_world, request(3), ierr)
            call MPI_Ialltoallw(plan%D_rd, plan%count_send, plan%displ_send, plan%ddtype_Fs, &
                                plan%D_rt, plan%count_recv, plan%displ_recv, plan%ddtype_Bs, &
                                plan%ptdma_world, request(4), ierr)

            call MPI_Waitall(4, request, MPI_STATUSES_IGNORE, ierr)

            plan%a_rt_d = plan%A_rt
            plan%b_rt_d = plan%B_rt
            plan%c_rt_d = plan%C_rt
            plan%d_rt_d = plan%D_rt
        endif

        ! Solve the reduced tridiagonal systems of equations using Thomas algorithm.
        threads = dim3(thread_in_x, thread_in_y, 1)
        blocks  = dim3(block_rt_in_x, block_in_y, 1)

        call tdma_many_cuda<<<blocks, threads>>>(plan%a_rt_d,plan%b_rt_d,plan%c_rt_d,plan%d_rt_d, n_row_rt)

        ! Transpose the obtained solutions to original reduced forms using MPI_Ialltoallw and DDTs.
        if(plan%ptdma_nprocs.eq.1) then
            plan%d_rd_d = plan%d_rt_d
        else
            plan%D_rt = plan%d_rt_d
            call MPI_Ialltoallw(plan%D_rt, plan%count_recv, plan%displ_recv, plan%ddtype_Bs, &
                                plan%D_rd, plan%count_send, plan%displ_send, plan%ddtype_Fs, &
                                plan%ptdma_world, request(1), ierr)
            call MPI_Waitall(1, request, MPI_STATUSES_IGNORE, ierr)
            plan%d_rd_d = plan%D_rd
        endif

        ! Update solutions of the modified tridiagonal system with the solutions of the reduced tridiagonal system.
        threads = dim3(thread_in_x, thread_in_y, 1)
        blocks  = dim3(block_in_x, block_in_y, 1)
        call PaScaL_TDMA_many_update_solution_cuda<<<blocks, threads>>>(a_d, c_d, d_d, plan%d_rd_d, n_row)

    end subroutine PaScaL_TDMA_many_solve_cuda



    !>
    !> @brief   Solve many cyclic tridiagonal systems of equations.
    !> @param   plan        Plan for many tridiagonal systems of equations
    !> @param   A           Coefficients in lower diagonal elements
    !> @param   B           Coefficients in diagonal elements
    !> @param   C           Coefficients in upper diagonal elements
    !> @param   D           Coefficients in right-hand side terms
    !> @param   n_sys       Number of tridiagonal systems per process
    !> @param   n_row       Number of rows in each process, size of a tridiagonal matrix N divided by nprocs
    !>
    subroutine PaScaL_TDMA_many_solve_cycle_cuda(plan, a_d, b_d, c_d, d_d, n_sys, n_row, n_blk)

        implicit none

        type(ptdma_plan_many_cuda), intent(inout)   :: plan
        integer, value, intent(in)                  :: n_sys, n_row, n_blk
        double precision, device, intent(inout)     :: a_d(n_sys,n_blk,n_row), b_d(n_sys,n_blk,n_row)
        double precision, device, intent(inout)     :: c_d(n_sys,n_blk,n_row), d_d(n_sys,n_blk,n_row)

        ! Temporary variables for computation and parameters for MPI functions.
        integer :: request(4), ierr
        integer :: n_row_rt, n_sys_rt
        type(dim3) :: blocks, threads

        n_row_rt = plan%n_row_rt
        n_sys_rt = plan%n_sys_rt

        ! Setting the thread and block
        block_in_x = n_sys/thread_in_x
        if(block_in_x.eq.0 .or. mod(n_sys, thread_in_x)) then
            print '(a,i5,a,i5)', '[Error] n_sys should be a multiple of thread_in_x. thread_in_x = ',thread_in_x,', n_sys = ',n_sys
            call MPI_Finalize(ierr)
            stop
        endif

        block_rt_in_x = n_sys_rt/thread_in_x
        if(block_rt_in_x.eq.0 .or. mod(n_sys_rt, thread_in_x)) then
            print '(a,i5,a,i5)', '[Error] n_sys_rt should be a multiple of thread_in_x. thread_in_x = ',thread_in_x,', n_sys_rt = ',n_sys_rt
            call MPI_Finalize(ierr)
            stop
        endif

        block_in_y = n_blk/thread_in_y
        if(block_in_y.eq.0 .or. mod(n_blk, thread_in_y)) then
            print '(a,i5,a,i5)', '[Error] n_blk should be a multiple of thread_in_y. thread_in_y = ',thread_in_y,', n_blk = ',n_blk
            call MPI_Finalize(ierr)
            stop
        endif

        threads = dim3(thread_in_x, thread_in_y, 1)
        blocks  = dim3(block_in_x, block_in_y, 1)

        ! The modified Thomas algorithm
        call PaScaL_TDMA_many_modified_Thomas_cuda<<<blocks, threads>>>(a_d, b_d, c_d, d_d, plan%a_rd_d, plan%b_rd_d, plan%c_rd_d, plan%d_rd_d, n_row)

        ! Transpose the reduced systems of equations for TDMA using MPI_Ialltoallw and DDTs.
        if(plan%ptdma_nprocs.eq.1) then
            plan%a_rt_d = plan%a_rd_d
            plan%b_rt_d = plan%b_rd_d
            plan%c_rt_d = plan%c_rd_d
            plan%d_rt_d = plan%d_rd_d
        else
            plan%A_rd = plan%a_rd_d
            plan%B_rd = plan%b_rd_d
            plan%C_rd = plan%c_rd_d
            plan%D_rd = plan%d_rd_d
        
            call MPI_Ialltoallw(plan%A_rd, plan%count_send, plan%displ_send, plan%ddtype_Fs, &
                                plan%A_rt, plan%count_recv, plan%displ_recv, plan%ddtype_Bs, &
                                plan%ptdma_world, request(1), ierr)
            call MPI_Ialltoallw(plan%B_rd, plan%count_send, plan%displ_send, plan%ddtype_Fs, &
                                plan%B_rt, plan%count_recv, plan%displ_recv, plan%ddtype_Bs, &
                                plan%ptdma_world, request(2), ierr)
            call MPI_Ialltoallw(plan%C_rd, plan%count_send, plan%displ_send, plan%ddtype_Fs, &
                                plan%C_rt, plan%count_recv, plan%displ_recv, plan%ddtype_Bs, &
                                plan%ptdma_world, request(3), ierr)
            call MPI_Ialltoallw(plan%D_rd, plan%count_send, plan%displ_send, plan%ddtype_Fs, &
                                plan%D_rt, plan%count_recv, plan%displ_recv, plan%ddtype_Bs, &
                                plan%ptdma_world, request(4), ierr)

            call MPI_Waitall(4, request, MPI_STATUSES_IGNORE, ierr)

            plan%a_rt_d = plan%A_rt
            plan%b_rt_d = plan%B_rt
            plan%c_rt_d = plan%C_rt
            plan%d_rt_d = plan%D_rt
        endif

        threads = dim3(thread_in_x, thread_in_y, 1)
        blocks  = dim3(block_rt_in_x, block_in_y, 1)

        ! Solve the reduced tridiagonal systems of equations using Thomas algorithm.
        call tdma_cycl_many_cuda<<<blocks, threads>>>(plan%a_rt_d,plan%b_rt_d,plan%c_rt_d,plan%d_rt_d,plan%e_rt_d, n_row_rt)

        ! Transpose the obtained solutions to original reduced forms using MPI_Ialltoallw and DDTs.
        if(plan%ptdma_nprocs.eq.1) then
            plan%d_rd_d = plan%d_rt_d
        else
            plan%D_rt = plan%d_rt_d
            call MPI_Ialltoallw(plan%D_rt, plan%count_recv, plan%displ_recv, plan%ddtype_Bs, &
                                plan%D_rd, plan%count_send, plan%displ_send, plan%ddtype_Fs, &
                                plan%ptdma_world, request(1), ierr)
            call MPI_Waitall(1, request, MPI_STATUSES_IGNORE, ierr)
            plan%d_rd_d = plan%D_rd
        endif

        threads = dim3(thread_in_x, thread_in_y, 1)
        blocks  = dim3(block_in_x, block_in_y, 1)
        ! Update solutions of the modified tridiagonal system with the solutions of the reduced tridiagonal system.
        call PaScaL_TDMA_many_update_solution_cuda<<<blocks, threads>>>(a_d, c_d, d_d, plan%d_rd_d, n_row)

    end subroutine PaScaL_TDMA_many_solve_cycle_cuda

    !>
    !> @brief   Modified Thomas algorithm
    !> @param   plan        Plan for many tridiagonal systems of equations
    !> @param   A           Coefficients in lower diagonal elements
    !> @param   B           Coefficients in diagonal elements
    !> @param   C           Coefficients in upper diagonal elements
    !> @param   D           Coefficients in right-hand side terms
    !> @param   n_sys       Number of tridiagonal systems per process
    !> @param   n_row       Number of rows in each process, size of a tridiagonal matrix N divided by nprocs
    !>
    attributes(global) subroutine PaScaL_TDMA_many_modified_Thomas_cuda(a, b, c, d, a_rd, b_rd, c_rd, d_rd, n_row)

        implicit none

        integer, value, intent(in)      :: n_row
        double precision, intent(inout) :: a(:,:,:), c(:,:,:), d(:,:,:)
        double precision, intent(in)    :: b(:,:,:)
        double precision, intent(inout)   :: a_rd(:,:,:), b_rd(:,:,:), c_rd(:,:,:), d_rd(:,:,:)

        ! Temporary variables for computation
        integer :: i, j, k
        integer :: ti, tj, tk
        double precision :: r

        ! Block shared memory
        double precision, shared :: a1(thread_in_x+1, thread_in_y), a0(thread_in_x+1, thread_in_y)
        double precision, shared :: b1(thread_in_x+1, thread_in_y), b0(thread_in_x+1, thread_in_y)
        double precision, shared :: c1(thread_in_x+1, thread_in_y), c0(thread_in_x+1, thread_in_y)
        double precision, shared :: d1(thread_in_x+1, thread_in_y), d0(thread_in_x+1, thread_in_y)
        double precision, shared :: r0(thread_in_x+1, thread_in_y)

        j = (blockidx%x-1) * blockdim%x + threadidx%x
        k = (blockidx%y-1) * blockdim%y + threadidx%y

        tj = threadidx%x
        tk = threadidx%y

        ! The modified Thomas algorithm : elimination of lower diagonal elements. 
        ! First index indicates a row number in a partitioned tridiagonal system .
        ! Second & Third index indicate a number of independent many tridiagonal systems for parallezation.
        ! The modified Thomas algorithm : elimination of lower diagonal elements. 

        a0(tj,tk) = a(j,k,1)
        b0(tj,tk) = b(j,k,1)
        c0(tj,tk) = c(j,k,1)
        d0(tj,tk) = d(j,k,1)

        a0(tj,tk) = a0(tj,tk) / b0(tj,tk)
        c0(tj,tk) = c0(tj,tk) / b0(tj,tk)
        d0(tj,tk) = d0(tj,tk) / b0(tj,tk)

        a(j,k,1) = a0(tj,tk)
        c(j,k,1) = c0(tj,tk)
        d(j,k,1) = d0(tj,tk)

        a1(tj,tk) = a(j,k,2)
        b1(tj,tk) = b(j,k,2)
        c1(tj,tk) = c(j,k,2)
        d1(tj,tk) = d(j,k,2)

        a1(tj,tk) = a1(tj,tk) / b1(tj,tk)
        c1(tj,tk) = c1(tj,tk) / b1(tj,tk)
        d1(tj,tk) = d1(tj,tk) / b1(tj,tk)

        a(j,k,2) = a1(tj,tk)
        c(j,k,2) = c1(tj,tk)
        d(j,k,2) = d1(tj,tk)


        do i=3, n_row

            a0(tj,tk) = a1(tj,tk)
            c0(tj,tk) = c1(tj,tk)
            d0(tj,tk) = d1(tj,tk)

            a1(tj,tk) = a(j,k,i)
            b1(tj,tk) = b(j,k,i)
            c1(tj,tk) = c(j,k,i)
            d1(tj,tk) = d(j,k,i)

            r0(tj,tk) =  1.d0/(b1(tj,tk)-a1(tj,tk)*c0(tj,tk))
            d1(tj,tk) =  r0(tj,tk)*(d1(tj,tk)-a1(tj,tk)*d0(tj,tk))
            c1(tj,tk) =  r0(tj,tk)*c1(tj,tk)
            a1(tj,tk) = -r0(tj,tk)*a1(tj,tk)*a0(tj,tk)

            a(j,k,i) = a1(tj,tk)
            c(j,k,i) = c1(tj,tk)
            d(j,k,i) = d1(tj,tk)

        enddo

        ! Construct many reduced tridiagonal systems per each rank. Each process has two rows of reduced systems.
        a_rd(j,k,2) = a1(tj,tk)
        b_rd(j,k,2) = 1.d0
        c_rd(j,k,2) = c1(tj,tk)
        d_rd(j,k,2) = d1(tj,tk)

        a1(tj,tk) = a0(tj,tk)
        c1(tj,tk) = c0(tj,tk)
        d1(tj,tk) = d0(tj,tk)

        ! The modified Thomas algorithm : elimination of upper diagonal elements.
        do i=n_row-2, 2, -1

            a0(tj,tk) = a(j,k,i)
            c0(tj,tk) = c(j,k,i)
            d0(tj,tk) = d(j,k,i)

            d0(tj,tk) = d0(tj,tk)-c0(tj,tk)*d1(tj,tk)
            a0(tj,tk) = a0(tj,tk)-c0(tj,tk)*a1(tj,tk)
            c0(tj,tk) =-c0(tj,tk)*c1(tj,tk)

            a1(tj,tk) = a0(tj,tk)
            c1(tj,tk) = c0(tj,tk)
            d1(tj,tk) = d0(tj,tk)

            a(j,k,i) = a0(tj,tk)
            c(j,k,i) = c0(tj,tk)
            d(j,k,i) = d0(tj,tk)
        enddo

        a0(tj,tk) = a(j,k,1)
        c0(tj,tk) = c(j,k,1)
        d0(tj,tk) = d(j,k,1)

        r0(tj,tk) = 1.d0/(1.d0-a1(tj,tk)*c0(tj,tk))
        d0(tj,tk) =  r0(tj,tk)*(d0(tj,tk)-c0(tj,tk)*d1(tj,tk))
        a0(tj,tk) =  r0(tj,tk)*a0(tj,tk)
        c0(tj,tk) = -r0(tj,tk)*c0(tj,tk)*c1(tj,tk)

        d(j,k,1) = d0(tj,tk)
        a(j,k,1) = a0(tj,tk)
        c(j,k,1) = c0(tj,tk)

        ! Construct many reduced tridiagonal systems per each rank. Each process has two rows of reduced systems.
        a_rd(j,k,1) = a0(tj,tk)
        b_rd(j,k,1) = 1.d0
        c_rd(j,k,1) = c0(tj,tk)
        d_rd(j,k,1) = d0(tj,tk)

    end subroutine PaScaL_TDMA_many_modified_Thomas_cuda

    !>
    !> @brief   Modified Thomas algorithm : Update solution
    !> @param   plan        Plan for many tridiagonal systems of equations
    !> @param   A           Coefficients in lower diagonal elements
    !> @param   C           Coefficients in upper diagonal elements
    !> @param   D           Coefficients in right-hand side terms
    !> @param   n_sys       Number of tridiagonal systems per process
    !> @param   n_row       Number of rows in each process, size of a tridiagonal matrix N divided by nprocs
    !>
    attributes(global) subroutine PaScaL_TDMA_many_update_solution_cuda(a, c, d, d_rd, n_row)

        implicit none

        integer, value, intent(in)      :: n_row
        double precision, intent(in)    :: a(:,:,:), c(:,:,:), d_rd(:,:,:)
        double precision, intent(inout) :: d(:,:,:)

        ! Temporary variables for computation
        integer :: i, j, k
        integer :: tj, tk

        ! Block shared memory
        double precision, shared :: ds(thread_in_x, thread_in_y), de(thread_in_x, thread_in_y)

        j = (blockidx%x-1) * blockdim%x + threadidx%x
        k = (blockidx%y-1) * blockdim%y + threadidx%y

        tj = threadidx%x
        tk = threadidx%y

        ds(tj, tk) = d_rd(j,k,1)
        de(tj, tk) = d_rd(j,k,2)
        call syncthreads()

        ! Update solutions of the modified tridiagonal system with the solutions of the reduced tridiagonal system.
        d(j,k,1)     = ds(tj, tk)
        d(j,k,n_row) = de(tj, tk)

        do i=2,n_row-1
            d(j,k,i) = d(j,k,i)-a(j,k,i)*ds(tj, tk)-c(j,k,i)*de(tj, tk)
        enddo

    end subroutine PaScaL_TDMA_many_update_solution_cuda


    !>
    !> @brief       Solve many tridiagonal systems of equations using the Thomas algorithm.
    !>              First index indicates the number of independent many tridiagonal systems to use vectorization.
    !>              Second index indicates the row number in the tridiagonal system .
    !> @param       a       Coefficient array in lower diagonal elements
    !> @param       b       Coefficient array in diagonal elements
    !> @param       c       Coefficient array in upper diagonal elements
    !> @param       d       Coefficient array in the right-hand side terms
    !> @param       n1      Number of rows in each process, size of the tridiagonal matrix N divided by nprocs
    !>
    attributes(global) subroutine tdma_many_cuda(a, b, c, d, n1)

        implicit none

        integer, value, intent(in)              :: n1
        double precision, device, intent(in)    :: a(:,:,:), b(:,:,:)
        double precision, device, intent(inout) :: c(:,:,:), d(:,:,:)
        
        integer :: i, j, k
        integer :: tj, tk
        double precision :: r

        double precision, shared :: a1(thread_in_x+1, thread_in_y)
        double precision, shared :: b1(thread_in_x+1, thread_in_y)
        double precision, shared :: c0(thread_in_x+1, thread_in_y), c1(thread_in_x, thread_in_y)
        double precision, shared :: d0(thread_in_x+1, thread_in_y), d1(thread_in_x, thread_in_y)

        j = (blockidx%x-1) * blockdim%x + threadidx%x
        k = (blockidx%y-1) * blockdim%y + threadidx%y

        tj = threadidx%x
        tk = threadidx%y

        b1(tj, tk) = b(j, k, 1)
        c1(tj, tk) = c(j, k, 1)
        d1(tj, tk) = d(j, k, 1)

        d1(tj, tk) = d1(tj, tk) / b1(tj, tk)
        c1(tj, tk) = c1(tj, tk) / b1(tj, tk)
 
        d(j,k,1)=d1(tj, tk)
        c(j,k,1)=c1(tj, tk)

        do i=2,n1
            c0(tj, tk) = c1(tj, tk)
            d0(tj, tk) = d1(tj, tk)

            a1(tj, tk) = a(j, k, i)
            b1(tj, tk) = b(j, k, i)
            c1(tj, tk) = c(j, k, i)
            d1(tj, tk) = d(j, k, i)

            r=1.d0/(b1(tj,tk)-a1(tj,tk)*c0(tj,tk))
            d1(tj,tk)=r*(d1(tj,tk)-a1(tj,tk)*d0(tj,tk))
            c1(tj,tk)=r*c1(tj,tk)

            d(j,k,i)=d1(tj,tk)
            c(j,k,i)=c1(tj,tk)

        enddo

        do i=n1-1,1,-1
            c0(tj,tk) = c(j,k,i)
            d0(tj,tk) = d(j,k,i)
            d0(tj,tk) = d0(tj,tk)-c0(tj,tk)*d1(tj,tk)
            d1(tj,tk) = d0(tj,tk)
            d(j,k,i)=d0(tj,tk)
        enddo

    end subroutine tdma_many_cuda



    !>
    !> @brief       Solve many cyclic tridiagonal systems of equations using the Thomas algorithm.
    !>              First index indicates the number of independent many tridiagonal systems to use vectorization.
    !>              Second index indicates the row number in the tridiagonal system.
    !> @param       a       Coefficient array in lower diagonal elements
    !> @param       b       Coefficient array in diagonal elements
    !> @param       c       Coefficient array in upper diagonal elements
    !> @param       d       Coefficient array in the right-hand side terms
    !> @param       n1      Number of rows in each process, size of the tridiagonal matrix N divided by nprocs
    !>
    attributes(global) subroutine tdma_cycl_many_cuda(a, b, c, d, e, n1)

        implicit none

        integer, value, intent(in)              :: n1
        double precision, device, intent(in)    :: a(:,:,:), b(:,:,:)
        double precision, device, intent(inout) :: c(:,:,:), d(:,:,:), e(:,:,:)

        integer :: i, j, k
        integer :: tj, tk
        ! double precision :: e(1:n1)
        double precision :: r

        double precision, shared :: a1(thread_in_x+1, thread_in_y)
        double precision, shared :: b1(thread_in_x+1, thread_in_y)
        double precision, shared :: c0(thread_in_x+1, thread_in_y), c1(thread_in_x, thread_in_y)
        double precision, shared :: d0(thread_in_x+1, thread_in_y), d1(thread_in_x, thread_in_y)
        double precision, shared :: e0(thread_in_x+1, thread_in_y), e1(thread_in_x, thread_in_y)

        j = (blockidx%x-1) * blockdim%x + threadidx%x
        k = (blockidx%y-1) * blockdim%y + threadidx%y

        tj = threadidx%x
        tk = threadidx%y

        do i=1,n1
            e(j,k,i)  = 0.0d0
        enddo
        e(j,k,2)  = -a(j,k,2)
        e(j,k,n1) = -c(j,k,n1)

        d1(tj,tk) = d(j,k,2)
        b1(tj,tk) = b(j,k,2)
        c1(tj,tk) = c(j,k,2)
        e1(tj,tk) = e(j,k,2)

        d1(tj,tk) = d1(tj,tk) / b1(tj,tk)
        c1(tj,tk) = c1(tj,tk) / b1(tj,tk)
        e1(tj,tk) = e1(tj,tk) / b1(tj,tk)

        d(j,k,2)  = d1(tj,tk)
        c(j,k,2)  = c1(tj,tk)
        e(j,k,2)  = e1(tj,tk)


        do i=3,n1
            c0(tj,tk) = c1(tj,tk)
            d0(tj,tk) = d1(tj,tk)
            e0(tj,tk) = e1(tj,tk)

            a1(tj,tk) = a(j,k,i)
            b1(tj,tk) = b(j,k,i)
            c1(tj,tk) = c(j,k,i)
            d1(tj,tk) = d(j,k,i)
            e1(tj,tk) = e(j,k,i)

            r        = 1.d0/(b1(tj,tk)-a1(tj,tk)*c0(tj,tk))
            d1(tj,tk)= r*(d1(tj,tk)-a1(tj,tk)*d0(tj,tk))
            e1(tj,tk)= r*(e1(tj,tk)-a1(tj,tk)*e0(tj,tk))

            c1(tj,tk)= r*c1(tj,tk)
            e(j,k,i) = e1(tj,tk)
            d(j,k,i) = d1(tj,tk)
            c(j,k,i) = c1(tj,tk)
        enddo

        do i=n1-1,2,-1

            c0(tj,tk) = c(j,k,i)
            d0(tj,tk) = d(j,k,i)
            e0(tj,tk) = e(j,k,i)

            d0(tj,tk) = d0(tj,tk)-c0(tj,tk)*d1(tj,tk)
            e0(tj,tk) = e0(tj,tk)-c0(tj,tk)*e1(tj,tk) 
            d1(tj,tk) = d0(tj,tk)
            e1(tj,tk) = e0(tj,tk)
            d(j,k,i)  = d0(tj,tk)
            e(j,k,i)  = e0(tj,tk)

        enddo

        a1(tj,tk) = a(j,k,1)
        b1(tj,tk) = b(j,k,1)
        c1(tj,tk) = c(j,k,1)
        d1(tj,tk) = d(j,k,1)
        e1(tj,tk) = e(j,k,n1)

        d1(tj,tk) = (d1(tj,tk)-a1(tj,tk)*d(j,k,n1)-c1(tj,tk)*d0(tj,tk))/(b1(tj,tk)+a1(tj,tk)*e1(tj,tk)+c1(tj,tk)*e0(tj,tk)) ! e0(tj,tk) = e(j,k,2)
        d(j,k,1) = d1(tj,tk)

        do i=2,n1
            e1(tj,tk) = e(j,k,i)
            d(j,k,i) = d(j,k,i) + d1(tj,tk)*e1(tj,tk)
        enddo

    end subroutine tdma_cycl_many_cuda

end module PaScaL_TDMA_cuda